#Command Example:
#snakemake --cluster "sbatch -p pi_dunn --time {params.time} --mem {params.mem} --nodes=1 --ntasks-per-node=15" --jobs 2 --cores 15
#SPECIES="sarsia"
#SAMPLES="x,x,x"
TRANSCRIPTOME="Trinity_20210112.fasta"
GENE_ID_TYPE="type_2"

rule all: # list all the targets to be generate by in the workflow
    input:
        expand("reference/{transcriptome}.transdecoder_dir/longest_orfs.pep", transcriptome=TRANSCRIPTOME),
        expand("reference/{transcriptome}_longestORFperGene.pep", transcriptome=TRANSCRIPTOME),
        expand("reference/{transcriptome}_longestORFperGene.fasta", transcriptome=TRANSCRIPTOME),
        expand("reference/{transcriptome}_longestORFperGene.eggnog.gtf",transcriptome=TRANSCRIPTOME)

rule generate_longest_ORFs:
    """
    Infer open reading frames in reference transcriptome.
    """
    input:
        "reference/{transcriptome}"
    output:
        "reference/{transcriptome}.transdecoder_dir/longest_orfs.pep"
    shell:
        "TransDecoder.LongOrfs -t {input} --output_dir reference/{wildcards.transcriptome}.transdecoder_dir"

rule keep_longest_ORF_per_gene:
    """
    Keep longest open reading frames per gene (as per gene identifier). 
    Details on functions and arguments in the python script itself.
    """
    input:
        longestORFs="reference/{transcriptome}.transdecoder_dir/longest_orfs.pep",
        transcriptomeFasta="reference/{transcriptome}",
        script="scripts/keepLongestORFperGene.py"
    output:
        peptides="reference/{transcriptome}_longestORFperGene.pep",
        nucleotides="reference/{transcriptome}_longestORFperGene.fasta"
    shell:
        "python {input.script} -p {input.longestORFs} -t "
        "{input.transcriptomeFasta} -identifier {GENE_ID_TYPE} -o reference"

rule make_GTF:
    input:
        nucleotides="reference/{transcriptome}_longestORFperGene.fasta",
        peptides="reference/{transcriptome}_longestORFperGene.pep",
        script="scripts/makeGTF_emapper.py"
    output:
        "reference/{transcriptome}_longestORFperGene.eggnog.gtf"
    threads: 15
    conda:
        "mapping.yaml"
    params:
        time="70:00:00",
        mem="100GB"
    shell:
        "python {input.script} {input.nucleotides} {input.peptides} {GENE_ID_TYPE} reference"